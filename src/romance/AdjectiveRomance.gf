incomplete concrete AdjectiveRomance of Adjective =
  CatRomance ** open CommonRomance, ResRomance, Prelude in {
  flags coding=utf8;
  lin

    PositA  a = {
      s = a.s ! Posit ;
      isPre = a.isPre
      } ;
    ComparA a np = {
      s = \\af => a.s ! Compar ! af ++ conjThan ++ (np.s ! Nom).ton ;
      isPre = False
      } ;
    CAdvAP ad ap np = {
      s = \\af => ad.s ++ ap.s ! af ++ ad.p ++ (np.s ! Nom).ton ;
      isPre = False
      } ;
    UseComparA a = {
      s = \\af => a.s ! Compar ! af ;
      isPre = a.isPre
      } ;
    AdjOrd ord = {
      s = \\af => ord.s ! aform2aagr af ; ----
      isPre = False ----
      } ;

-- $SuperlA$ belongs to determiner syntax in $Noun$.

    ComplA2 adj np = {
      s = \\af => adj.s ! Posit ! af ++ appCompl adj.c2 np ;
      isPre = False
      } ;

    ReflA2 adj = {
      s = \\af =>
             adj.s ! Posit ! af ++
             adj.c2.s ++ prepCase adj.c2.c ++ reflPron Sg P3 Nom ; --- agr
      isPre = False
      } ;

    SentAP ap sc = {
      s = \\a => ap.s ! a ++ sc.s ! dative ; -- prête à dormir --- mood
      isPre = False
      } ;

    AdAP ada ap = {
      s = \\a => ada.s ++ ap.s ! a ;
      isPre = ap.isPre
      } ;

    UseA2 a = {
      s = a.s ! Posit ;
      isPre = False ---- A2 has no isPre
      } ;

    AdvAP ap adv = {
      s = \\a => ap.s ! a ++ adv.s ;
      isPre = False
      } ;


}
