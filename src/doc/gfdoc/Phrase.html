<html>
<body>
<i> Produced by 
gfdoc - a rudimentary GF document generator.
(c) Aarne Ranta (aarne@cs.chalmers.se) 2002 under GNU GPL.
 </i>
<p>
<h1> Phrase: Phrases and Utterances</h1>

<pre>
  abstract Phrase = Cat ** {
</pre>

When a phrase is built from an utterance it can be prefixed
with a phrasal conjunction (such as <i>but</i>, <i>therefore</i>)
and suffixing with a vocative (typically a noun phrase).
<pre>
    fun
      PhrUtt   : PConj -> Utt -> Voc -> Phr ; -- but come here, my friend
</pre>

Utterances are formed from sentences, questions, and imperatives.
<pre>
      UttS      : S   -> Utt ;                -- John walks
      UttQS     : QS  -> Utt ;                -- is it good
      UttImpSg  : Pol -> Imp -> Utt;          -- (don't) love yourself
      UttImpPl  : Pol -> Imp -> Utt;          -- (don't) love yourselves
      UttImpPol : Pol -> Imp -> Utt ;         -- (don't) sleep (polite)
</pre>

There are also 'one-word utterances'. A typical use of them is
as answers to questions.
<b>Note</b>. This list is incomplete. More categories could be covered.
Moreover, in many languages e.g. noun phrases in different cases
can be used.
<pre>
      UttIP     : IP   -> Utt ;               -- who
      UttIAdv   : IAdv -> Utt ;               -- why
      UttNP     : NP   -> Utt ;               -- this man
      UttAdv    : Adv  -> Utt ;               -- here
      UttVP     : VP   -> Utt ;               -- to sleep
      UttCN     : CN   -> Utt ;               -- house
      UttCard   : Card -> Utt ;               -- five
      UttAP     : AP   -> Utt ;               -- fine
      UttInterj : Interj -> Utt ;             -- alas
</pre>

The phrasal conjunction is optional. A sentence conjunction
can also be used to prefix an utterance.
<pre>
      NoPConj   : PConj ;                      
      PConjConj : Conj -> PConj ;             -- and
</pre>

The vocative is optional. Any noun phrase can be made into vocative,
which may be overgenerating (e.g. <i>I</i>).
<pre>
      NoVoc   : Voc ;
      VocNP   : NP -> Voc ;                   -- my friend
  
  }
</pre>

</body>
</html>
